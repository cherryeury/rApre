//===== rAthena Script =======================================
//= Euphy's Quest Shop
//===== By: ==================================================
//= Euphy
//===== Current Version: =====================================
//= 1.6c
//===== Compatible With: =====================================
//= rAthena Project
//===== Description: =========================================
//= A dynamic quest shop based on Lunar's, with easier config.
//= Includes support for multiple shops & cashpoints.
//= Item Preview script by ToastOfDoom.
//===== Additional Comments: =================================
//= 1.0 Initial script.
//= 1.2 Added category support.
//= 1.3 More options and fixes.
//= 1.4 Added debug settings.
//= 1.5 Replaced categories with shop IDs.
//= 1.6 Added support for purchasing stackables.
//= 1.6a Added support for previewing costumes and robes.
//= 1.6b Added 'disable_items' command.
//= 1.6c Replaced function 'A_An' with "F_InsertArticle".
//============================================================

// Shop NPCs -- supplying no argument displays entire menu.
//	callfunc "iopshop"{,<shop ID>{,<shop ID>{,...}}};
//  ADD YOUR NPC HERE
//============================================================
aldebaran,145,119,5	script	Nyan#1	4_CAT_SAILOR1,{ callfunc "iopshop",1; }
rachel,121,147,5	script	Nyan#2	4_CAT_SAILOR1,{ callfunc "iopshop",2; }
alberta,30,229,0	script	Nyan#3	4_CAT_SAILOR1,{ callfunc "iopshop",3; }
lighthalzen,167,102,5	script	Nyan#4	4_CAT_SAILOR1,{ callfunc "iopshop",4; }
morocc,164,97,5	script	Nyan#5	4_CAT_SAILOR1,{ callfunc "iopshop",5; }
payon,183,103,5	script	Nyan#6	4_CAT_SAILOR1,{ callfunc "iopshop",6; }
yuno,150,187,5	script	Nyan#7	4_CAT_SAILOR1,{ callfunc "iopshop",7; }
geffen,112,64,5	script	Nyan#8	4_CAT_SAILOR1,{ callfunc "iopshop",8; }
comodo,200,149,5	script	Nyan#9	4_CAT_SAILOR1,{ callfunc "iopshop",9; }
gef_fild10,76,340,5	script	Nyan#10	4_CAT_SAILOR1,{ callfunc "iopshop",10; }
prontera,160,189,5	script	Nyan#11	4_CAT_SAILOR1,{ callfunc "iopshop",11; }
xmas,150,136,5	script	Nyan#12	4_CAT_SAILOR1,{ callfunc "iopshop",12; }
moscovia,220,191,5	script	Nyan#13	4_CAT_SAILOR1,{ callfunc "iopshop",13; }
hugel,85,168,5	script	Nyan#14	4_CAT_SAILOR1,{ callfunc "iopshop",14; }
einbech,182,136,5	script	Nyan#15	4_CAT_SAILOR1,{ callfunc "iopshop",15; }
glast_01,366,308,5	script	Nyan#16	4_CAT_SAILOR1,{ callfunc "iopshop",16; }
gonryun,156,122,5	script	Nyan#17	4_CAT_SAILOR1,{ callfunc "iopshop",17; }
izlude,125,148,5	script	Nyan#18	4_CAT_SAILOR1,{ callfunc "iopshop",18; }
//============================================================


// Script Core - DO NOT DUPLICATE THIS NPC !!!!!!!!!!!!!
//============================================================
-	script	wuest_shop	-1,{
function Add; function Chk; function Slot;
OnInit:
	freeloop(1);

// -----------------------------------------------------------
//  Basic shop settings.
// -----------------------------------------------------------

	set .Announce,0;	// Announce quest completion? (1: yes / 0: no)
	set .ShowSlot,1;	// Show item slots? (2: all equipment / 1: if slots > 0 / 0: never)
	set .ShowID,0;  	// Show item IDs? (1: yes / 0: no)
	set .ShowZeny,1;	// Show Zeny cost, if any? (1: yes / 0: no)
	set .MaxStack,100;	// Max number of quest items purchased at one time.

// -----------------------------------------------------------
//  Points variable -- optional quest requirement.
//	setarray .Points$[0],"<variable name>","<display name>";
// -----------------------------------------------------------

	setarray .Points$[0],
		"#CASHPOINTS", "Cash Points";


//=====================================================================================
// ------------------- ADD YOUR SHOPS NAME AND ITEMS SHOPS STARTING HERE --------------
//=====================================================================================

// -----------------------------------------------------------
//  Shop IDs -- to add shops, copy dummy data at bottom of file.
//	setarray .Shops$[1],"<Shop 1>","<Shop 2>"{,...};
// -----------------------------------------------------------

	setarray .Shops$[1],
		"Aldebaran",	//
		"Rachel",	//
		"Alberta",	//
		"Lighthalzen",	//
		"Morocc", //
		"Payon", //
		"Yuno", //
		"Geffen", //
		"Comodo", //
		"Orc", //
		"Prontera", //
		"Lutie", // 
		"Moscovia", // 
		"Hugel", // 
		"Einbech", // 
		"Glastheim", //
		"Kunlun", // 
		"Izlude"; // 
		

// -----------------------------------------------------------
//  Quest items -- do NOT use a reward item more than once!
//	Add(<shop ID>,<reward ID>,<reward amount>,
//	    <Zeny cost>,<point cost>,
//	    <required item ID>,<required item amount>{,...});
// -----------------------------------------------------------

//Aldebaran
	Add(1,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(1,5086,1,0,0,1095,3000,2288,1); // Alarm Mask
	Add(1,2273,1,0,0,2275,1,998,50,733,1); // Doctor Band
	Add(1,5018,1,0,0,2247,1,916,300); // Feather Bonnet
	Add(1,2281,1,0,0,998,20,707,1); // Opera Mask
	Add(1,2280,1,0,0,1019,120); // Sakkat
//Rachel	
	Add(2,5170,1,0,0,5172,1,7063,100,982,1); // Feather Beret
	Add(2,5175,1,0,0,938,100,7111,99,983,1); // Censor Bar
//Alberta
	Add(3,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(3,2284,1,0,0,923,20); // Antler
	Add(3,5016,1,0,0,1030,10); // Boy's Cap
	Add(3,5042,1,0,0,10007,1,968,50); // Bao Bao
	Add(3,5041,1,0,0,7200,1200); // Heart Hairpin
	Add(3,5047,1,0,0,2271,1,975,1); // Fashionable Glasses
	Add(3,5048,1,0,0,5041,1,999,10); // Crescent Hairpin	
	Add(3,5022,1,0,0,5022,1,6242,750,6625,500); // Hat of Sun God (todo)
	Add(3,5032,1,0,0,1059,250,2221,1,2227,1,7063,600); // Sunday Hat
	Add(3,5027,1,0,0,2252,1,1036,400,7001,50,4052,1); // Mage Hat
	Add(3,5045,1,0,0,2252,1,1054,450,943,1200); // Magician Hat	
	Add(3,5443,1,0,0,2252,1,1054,450,943,1200); // Statue
	Add(3,2214,1,0,0,949,100,706,1,722,1,2213,1); // Bunny Band (everywhere)
	Add(3,5448,1,0,0,2252,1,1054,450,943,1200); // Solo Hat
//Lighthalzen
	Add(4,5110,1,0,0,941,2,7270,1,526,1,10004,1); // Pacifier
	Add(4,5117,1,0,0,6242,1,731,10,748,3,982,1); // Mystic Rose
	Add(4,5109,1,0,0,6242,1,10015,1,10007,1,975,1,5032,1); // Red Bonnet
	Add(4,5442,1,0,0,978,1,7151,30,1059,100,7217,20); // Necktie
	Add(4,5108,1,0,0,2244,1,2209,1,10007,1); // Renown detective
//Morocc
	Add(5,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(5,2296,1,0,0,2243,1,999,100); // Binoculars
	Add(5,2272,1,0,0,6242,1,1019,50,983,1); // Stop Post
	Add(5,5058,1,0,0,2233,1,983,1,7206,300,7030,1); // Drooping Cat
	Add(5,5084,1,0,0,1026,1000,7065,100,945,10,7030,1); // Lazy Smokie
	Add(5,5064,1,0,0,945,600,7030,1); // Smokie Leaf	
	Add(5,5065,1,0,0,624,1,959,300,551,50,1023,1,938,100,7030,1); // Blue Fish
	Add(5,2214,1,0,0,949,100,706,1,722,1,2213,1); // Bunny Band (everywhere)
//Payon
	Add(6,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(6,5073,1,0,0,2285,1,1550,1); // Model Training Hat
	Add(6,5057,1,0,0,2213,1,983,1,914,300); // Ears of Black Cat
	Add(6,2283,1,0,0,724,1,5001,1,949,200); // Ear Muffs
	Add(6,5069,1,0,0,1022,999); // Kitsune mask
	Add(6,5004,1,0,0,701,5); // Oxygen Mask
	Add(6,2293,1,0,0,710,1,703,1,704,1,708,1); // Murderer
	Add(6,5025,1,0,0,2229,1,2254,1,7036,5); // Helm of Angel
	Add(6,5038,1,0,0,1038,600,7048,40); // Deviruchi
	Add(6,5173,1,0,0,7445,7,7446,7,7447,7,7448,7,954,300,983,1,733,5,2221,1); // Magistrate Hat
	Add(6,5174,1,0,0,7445,7,7446,7,7447,7,7448,7,954,300,975,1,733,5,7166,1); // Ayam
	Add(6,5176,1,0,0,706,1,7201,500,7200,10,7015,20,952,100,1028,100); // Hahoe	
	Add(6,5177,1,0,0,706,1,7201,500,7200,10,1048,500,1053,2,980,1); // Lion Mask
	Add(6,5169,1,0,0,706,1,7201,500,7200,10,1049,20,1059,500,1054,2,1024,100); // Bride Mask
	Add(6,2214,1,0,0,949,100,706,1,722,1,2213,1); // Bunny Band (everywhere)
//Yuno
	Add(7,5107,1,0,0,519,50,7031,50,548,50,539,50); // Crunch Toast
	Add(7,5074,1,0,0,2254,1,2286,1,6242,1); // Angel Wing Ears	
	Add(7,5068,1,0,0,6242,1,2255,1,2286,1); // Evil Wing Ears
	Add(7,5091,1,0,0,10016,1,714,1,969,3); // Big Golden Bell
	Add(7,5080,1,0,0,10006,1,714,1,969,3); // Crown of Ancient Queen
	Add(7,5081,1,0,0,2249,1,714,1,969,3); // Crown of Mistress	
	Add(7,5070,1,0,0,7216,300,7097,300,2211,1,982,1); // Hot Blooded
//Geffen
	Add(8,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(8,5001,1,0,0,999,40,984,1,970,1,1003,1); // Headset	
	Add(8,5082,1,0,0,921,300); // Decorative Mushroom
	Add(8,5077,1,0,0,2278,1,975,1); // Tulip Hairpin
	Add(8,2292,1,0,0,999,50); // Welding Mask
	Add(8,5079,1,0,0,2294,1,7220,400); // X Hairpin
	Add(8,5063,1,0,0,970,1,930,500); // Giant Band Aid
	Add(8,5061,1,0,0,2269,1,999,10); // Flower Hairpin
	Add(8,5446,1,0,0,5446,1); // Catfoot Hairpin 
	Add(8,5447,1,0,0,5447,1); // Frog Hat 
	Add(8,2214,1,0,0,949,100,706,1,722,1,2213,1); // Bunny Band (everywhere)
//Comodo
	Add(9,5071,1,0,0,5010,1,5049,1,7101,1); // Indian Headband	
	Add(9,5036,1,0,0,2608,1,7069,500); // Cross Hat
	Add(9,5034,1,0,0,2233,1,746,20); // Bulb Headband
	Add(9,5049,1,0,0,1099,1500); // Striped Hairband
	Add(9,5052,1,0,0,2211,1,978,1,7003,300); // Blue Hairband
	Add(9,5031,1,0,0,5009,1,5028,1,747,1,999,25); // Mine Helmet
	Add(9,5023,1,0,0,1059,150,907,100,978,1); // Parcel Hat	
	Add(9,5021,1,0,0,2233,1,969,1,999,20,949,80,938,800); // Bankruptcy for The heart
	Add(9,5447,1,0,0,2281,1,1048,50); // Opera Phantom Mask		
	Add(9,5444,1,0,0,974,1,975,1,943,50,985,5,1302,1); // Red Comb
//Orc
	Add(10,5094,1,0,0,5094,1,6242,750,6625,500); // Helmet of Orc Hero 
	Add(10,5436,1,0,0,1032,1000,10007,1); // Bride's Corolla
//Prontera
	Add(11,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)
	Add(11,5012,1,0,0,710,1,703,1,704,1,708,1); // Ph.D Hat
	Add(11,5083,1,0,0,2244,1,2209,1,10007,1); // Red Ribbon
	Add(11,2213,1,0,0,949,100,706,1,722,1,2213,1); // Bunny Band (everywhere)
//Lutie
	Add(12,5078,1,0,0,5033,1,5064,1); // Sea-Otter Hat
	Add(12,5059,1,0,0,5030,1,7213,100,7217,100,7161,300); // Teddybear Hat
	Add(12,5040,1,0,0,5030,1,7213,100,7217,100,7161,300); // Blush
	Add(12,5024,1,0,0,529,10,530,5,539,20,999,10,538,15); // Cake Hat
	Add(12,5028,1,0,0,2279,1,7035,50,526,100); // Candle
	Add(12,5026,1,0,0,1036,450,949,330,539,120,982,1); // Chef Hat
	Add(12,5033,1,0,0,1036,20,2213,1,7065,300,7012,200); // Raccoon Hat
	Add(12,5039,1,0,0,7030,1,978,1,5015,1); // Rainbow Eggshell
	Add(12,5029,1,0,0,7068,300,7033,850,1015,1); // Spore Hat
	Add(12,5050,1,0,0,7068,300,7033,850,1015,1); // Wonder	
	Add(12,5062,1,0,0,2280,1,7197,300,7150,300); // Straw Hat
	Add(12,5075,1,0,0,2248,1,7030,108,7194,108,7120,4); // Cowboy Hat
	Add(12,5067,1,0,0,5062,1,952,50,1907,1); // Sombrero
	Add(12,5076,1,0,0,2226,1,7038,500); // Beanie	
//Moscovia
	Add(13,5243,1,0,0,1022,20,7038,10,7166,10,7065,20,7217,1); // Shafka
//Hugel
	Add(14,5171,1,0,0,6242,100,4219,1,4114,1,4177,1,4259,1,4212,1,4073,1,4112,1,4081,1,4251,1,4166,1,7511,1000,7563,1000); // Valkyrie Helm	
//Einbech
	Add(15,5115,1,0,0,6242,1,983,1,7267,999,749,1); // Winter Hat
//Glastheim
	Add(16,5121,1,0,0,5121,1,6242,750,6625,500); // Zealotus 
//Kunlun	
	Add(17,5437,1,0,0,7165,100,7166,100,745,1,981,1,2211,1); // Celestial Flower
//Izlude	
	Add(18,2278,1,0,0,949,100,706,1,722,1,2213,1); // Mr Smile (everywhere)

// -----------------------------------------------------------

//=====================================================================================
// ------------------- YOUR SHOPS AND ITEMS SHOPS HAVE BEEN ADDED ---------------------
//=====================================================================================

	freeloop(0);
	set .menu$,"";
	for(set .@i,1; .@i<=getarraysize(.Shops$); set .@i,.@i+1) {
		set .menu$, .menu$+.Shops$[.@i]+":";
		npcshopdelitem "iopshop"+.@i,909;
	}
	end;

OnMenu:

	mes "[^FF6600Nyan^000000]";
	mes "Hello!";
	mes "I have the list of headgear quests";
	mes "around in this area!";
	next;
	mes "[^FF6600Nyan^000000]";
	mes "You can't buy from me.";
	mes "This is just a catalogue.";
	mes "You have to look around the area to get these.";
	mes "And there's some quests headgear that's not listed.";
	
	set .@size, getarraysize(@i);
	if (!.@size) set @shop_index, select(.menu$);
	else if (.@size == 1) set @shop_index, @i[0];
	else {
		for(set .@j,0; .@j<.@size; set .@j,.@j+1)
			set .@menu$, .@menu$+.Shops$[@i[.@j]]+":";
		set @shop_index, @i[select(.@menu$)-1];
	}
	deletearray @i[0],getarraysize(@i);
	if (.Shops$[@shop_index] == "") {
		message strcharinfo(0),"An error has occurred.";
		end;
	}
	dispbottom "This is a catalogue, you can't actually buy the items.";
	callshop "iopshop"+@shop_index,1;
	npcshopattach "iopshop"+@shop_index;
	end;

OnBuyItem:
	mes "[^FF6600Nyan^000000]";
	mes "Deal! Eh ? but..";
	mes "You cannot buy this, this is just a catalogue.";
	close;
	// .@q[] : RewardID, BoughtAmt, RewardAmt, BaseAmt, ReqZeny, ReqPts, { ReqItem, ReqAmt, ... }
	setarray .@q[0],@bought_nameid[0],((@bought_quantity[0] > .MaxStack)?.MaxStack:@bought_quantity[0]);
	copyarray .@q[3],getd(".q_"+@shop_index+"_"+.@q[0]+"[0]"),getarraysize(getd(".q_"+@shop_index+"_"+.@q[0]));
	set .@q[2],.@q[1]*.@q[3];
	if (!.@q[2] || .@q[2] > 30000) {
		message strcharinfo(0),"You can't purchase that many "+getitemname(.@q[0])+".";
		end;
	}
	mes "[^FF6600Nyan^000000]";
	mes "Reward: ^0055FF"+((.@q[2] > 1)?.@q[2]+"x ":"")+Slot(.@q[0])+"^000000";
	mes "Requirements:";
	disable_items;
	if (.@q[4]) mes " > "+Chk(Zeny,.@q[4]*.@q[1])+(.@q[4]*.@q[1])+" Zeny^000000";
	if (.@q[5]) mes " > "+Chk(getd(.Points$[0]),.@q[5]*.@q[1])+(.@q[5]*.@q[1])+" "+.Points$[1]+" ("+getd(.Points$[0])+"/"+(.@q[5]*.@q[1])+")^000000";
	if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
		mes " > "+Chk(countitem(.@q[.@i]),.@q[.@i+1]*.@q[1])+((.ShowID)?"{"+.@q[.@i]+"} ":"")+Slot(.@q[.@i])+" ("+countitem(.@q[.@i])+"/"+(.@q[.@i+1]*.@q[1])+")^000000";
	next;
	setarray @qe[1], getiteminfo(.@q[0], ITEMINFO_LOCATIONS), getiteminfo(.@q[0], ITEMINFO_VIEW);
	if (@qe[2] > 0 && ((@qe[1] & EQP_HEAD_LOW) || (@qe[1] & EQP_HEAD_TOP) || (@qe[1] & EQP_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_TOP) || (@qe[1] & EQP_COSTUME_HEAD_MID) || (@qe[1] & EQP_COSTUME_HEAD_LOW) || (@qe[1] & EQP_GARMENT) || (@qe[1] & EQP_COSTUME_GARMENT)))
		set .@preview,1;
	addtimer 1000, strnpcinfo(0)+"::OnEnd";
	while(1) {
		switch(select(" ~ Purchase ^0055FF"+getitemname(.@q[0])+"^000000:"+((.@preview && !@qe[7])?" ~ Preview...":"")+": ~ ^777777Cancel^000000")) {
		case 1:
			if (@qe[0]) {
				mes "[^FF6600Nyan^000000]";
				mes "You're missing one or more quest requirements.";
				close;
			}
			if (!checkweight(.@q[0],.@q[2])) {
				mes "[^FF6600Nyan^000000]";
				mes "^FF0000You need "+(((.@q[2]*getiteminfo(.@q[0], ITEMINFO_WEIGHT))+Weight-MaxWeight)/10)+" additional weight capacity to complete this trade.^000000";
				close;
			}
			if (.@q[4]) set Zeny, Zeny-(.@q[4]*.@q[1]);
			if (.@q[5]) setd .Points$[0], getd(.Points$[0])-(.@q[5]*.@q[1]);
			if (.@q[6]) for(set .@i,6; .@i<getarraysize(.@q); set .@i,.@i+2)
				delitem .@q[.@i],.@q[.@i+1]*.@q[1];
			getitem .@q[0],.@q[2];
			if (.Announce) announce strcharinfo(0)+" has created "+((.@q[2] > 1)?.@q[2]+"x "+getitemname(.@q[0]):callfunc("F_InsertArticle",getitemname(.@q[0])))+"!",0;
			specialeffect2 EF_FLOWERLEAF;
			close;
		case 2:
			setarray @qe[3], getlook(LOOK_HEAD_BOTTOM), getlook(LOOK_HEAD_TOP), getlook(LOOK_HEAD_MID), getlook(LOOK_ROBE), 1;
			if ((@qe[1] & 1) || (@qe[1] & 4096)) changelook LOOK_HEAD_BOTTOM, @qe[2];
			else if ((@qe[1] & 256) || (@qe[1] & 1024)) changelook LOOK_HEAD_TOP, @qe[2];
			else if ((@qe[1] & 512) || (@qe[1] & 2048)) changelook LOOK_HEAD_MID, @qe[2];
			else if ((@qe[1] & 4) || (@qe[1] & 8192)) changelook LOOK_ROBE, @qe[2];
			break;
		case 3:
			close;
		}
	}

OnEnd:
	if (@qe[7]) {
		changelook LOOK_HEAD_BOTTOM, @qe[3];
		changelook LOOK_HEAD_TOP, @qe[4];
		changelook LOOK_HEAD_MID, @qe[5];
		changelook LOOK_ROBE, @qe[6];
	}
	deletearray @qe[0],8;
	end;

function Add {
	if (getitemname(getarg(1)) == "null") {
		debugmes "Quest reward #"+getarg(1)+" invalid (skipped).";
		return;
	}
	setarray .@j[0],getarg(2),getarg(3),getarg(4);
	for(set .@i,5; .@i<getargcount(); set .@i,.@i+2) {
		if (getitemname(getarg(.@i)) == "null") {
			debugmes "Quest requirement #"+getarg(.@i)+" invalid (skipped).";
			return;
		} else
			setarray .@j[.@i-2],getarg(.@i),getarg(.@i+1);
	}
	copyarray getd(".q_"+getarg(0)+"_"+getarg(1)+"[0]"),.@j[0],getarraysize(.@j);
	npcshopadditem "iopshop"+getarg(0),getarg(1),((.ShowZeny)?getarg(3):0);
	return;
}

function Chk {
	if (getarg(0) < getarg(1)) {
		set @qe[0],1;
		return "^FF0000";
	} else
		return "^00FF00";
}

function Slot {
	set .@s$,getitemname(getarg(0));
	switch(.ShowSlot) {
		case 1: if (!getitemslots(getarg(0))) return .@s$;
		case 2: if (getiteminfo(getarg(0), ITEMINFO_TYPE) == 4 || getiteminfo(getarg(0), ITEMINFO_TYPE) == 5) return .@s$+" ["+getitemslots(getarg(0))+"]";
		default: return .@s$;
	}
}
}

function	script	iopshop	{
	deletearray @i[0],getarraysize(@i);
	for(set .@i,0; .@i<getargcount(); set .@i,.@i+1)
		set @i[.@i],getarg(.@i);
	doevent "wuest_shop::OnMenu";
	end;
}


// Dummy shop data -- copy as needed.
//============================================================
-	shop	iopshop1	-1,909:-1
-	shop	iopshop2	-1,909:-1
-	shop	iopshop3	-1,909:-1
-	shop	iopshop4	-1,909:-1
-	shop	iopshop5	-1,909:-1
-	shop	iopshop6	-1,909:-1
-	shop	iopshop7	-1,909:-1
-	shop	iopshop8	-1,909:-1
-	shop	iopshop9	-1,909:-1
-	shop	iopshop10	-1,909:-1
-	shop	iopshop11	-1,909:-1
-	shop	iopshop12	-1,909:-1
-	shop	iopshop13	-1,909:-1
-	shop	iopshop14	-1,909:-1
-	shop	iopshop15	-1,909:-1
-	shop	iopshop16	-1,909:-1
-	shop	iopshop17	-1,909:-1
-	shop	iopshop18	-1,909:-1